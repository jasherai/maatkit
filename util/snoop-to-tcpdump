#!/usr/bin/env perl

=pod

=head1 NAME

snoop-to-tcpdump

=head1 SYNOPSIS

  snoop -ta -r -x0 port 3306 | snoop-to-tcpdump > tcpdump-output.txt

=head1 AUTHOR

Daniel Nichter

=head1 LICENSE

This software is released to the public domain, with no guarantees whatsoever.

=cut

use strict;
use warnings FATAL => 'all';
use English qw(-no_match_vars);

my $file = shift @ARGV || '-';
my $fh;
if ( $file eq '-' ) {
   $fh = *STDIN;
}
else {
   open $fh, "<", $file or die "Cannot open $file: $OS_ERROR";
}

local $INPUT_RECORD_SEPARATOR = '';

my ($header, $packet);

while ( my $text = <$fh> ) {
   if ( !$header ) {
      ($header) = split(/\n/, $text);
      $header =~ s/^(\S+)\s+(\S+) -> (\S+)\s+TCP D=(\S+) S=(\S+)(.+)/2009-01-01 $1 IP $2.$5 > $3.$4: tcp 0/;
      $header .= "\n";
      next;
   }

   chomp $text;
   $text =~ s/^(\s+)\d+:\s+/\t0x0000:  /mgs;
   $packet = $header . $text;
   print "$packet\n";
   
   $header = '';
   $packet = '';
}

close $fh or warn "Cannot close $file: $OS_ERROR";

exit;
